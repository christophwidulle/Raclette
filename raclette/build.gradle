apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
    }
    dataBinding {
        enabled = true
    }
    lintOptions {
        abortOnError false
    }
    buildToolsVersion '27.0.3'
}

dependencies {

    api project(":raclette-base")

    implementation deps.supportv4
    api deps.supportAppCompatv7
    implementation deps.supportRecycler
    implementation deps.supportAnnotations
    implementation deps.javapoet

    testImplementation deps.junit
    testImplementation deps.truth
    testImplementation deps.robolectric
}

apply from: rootProject.file('gradle/gradle-maven-push.gradle')

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}


publishing.publications {
    maven(MavenPublication) {
        groupId GROUP
        artifactId POM_ARTIFACT_ID
        version VERSION_NAME

        artifact sourceJar
        artifact("${buildDir}/outputs/aar/${artifactId}-release.aar")

        //The publication doesn't know about our dependencies, so we have to manually add them to the pom
        pom.withXml {
            def dependenciesNode = asNode().appendNode('dependencies')

            //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
            configurations.compile.allDependencies.each {
                def dependencyNode = dependenciesNode.appendNode('dependency')
                dependencyNode.appendNode('groupId', it.group)
                dependencyNode.appendNode('artifactId', it.name)
                dependencyNode.appendNode('version', it.version)
            }
        }
    }
}

publishing.repositories {
    maven {
        url artifactory_url
        credentials {
            username = artifactory_user
            password = artifactory_password
        }
    }
}

