subprojects { project ->
    group = GROUP
    version = VERSION_NAME

    repositories {
        mavenCentral()
        mavenLocal()
    }

    apply plugin: 'checkstyle'

    task checkstyle(type: Checkstyle) {
        configFile rootProject.file('checkstyle.xml')
        source 'src/main/java'
        ignoreFailures false
        showViolations true
        include '**/*.java'

        classpath = files()
    }

    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('checkstyle')
        }
    }

    buildscript {
        repositories {
            jcenter()
            mavenLocal()

            maven {
                url "https://oss.sonatype.org/content/repositories/snapshots"
            }
            maven { url "https://jitpack.io" }
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:2.1.2'
            classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        }
    }
}
allprojects {
    tasks.withType(Javadoc).all { enabled = false }
}

ext {
    minSdkVersion = 14
    targetSdkVersion = 23
    compileSdkVersion = 23
    buildToolsVersion = '23.0.2'
    sourceCompatibilityVersion = JavaVersion.VERSION_1_7
    targetCompatibilityVersion = JavaVersion.VERSION_1_7
}

ext.deps = [
        // Android
        android            : 'com.google.android:android:4.0.1.2',
        supportv4          : 'com.android.support:support-v4:23.0.0',
        supportAnnotations : 'com.android.support:support-annotations:23.3.0',
        supportAppCompatv7 : 'com.android.support:appcompat-v7:23.3.0',
        supportRecycler    : 'com.android.support:recyclerview-v7:23.3.0',

        androidDatabinding : 'com.android.databinding:baseLibrary:2.0.0',
        androidDatabindingC: 'com.android.databinding:compilerCommon:2.0.0',

        // Square
        javapoet           : 'com.squareup:javapoet:1.6.1',

        // Other
        rxjava             : 'io.reactivex:rxjava:1.1.6',
        rxAndroid          : 'io.reactivex:rxandroid:1.2.1',
        rxRelay            : 'com.jakewharton.rxrelay:rxrelay:1.1.0',
        rxLifecycle        : 'com.trello:rxlifecycle:0.5.0',
        rxBinding          : 'com.jakewharton.rxbinding:rxbinding:0.4.0',
        rxBindingSupport   : 'com.jakewharton.rxbinding:rxbinding-support-v4:0.4.0',
        rxBindingRecycler  : 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.4.0',
        rxBindingDesign    : 'com.jakewharton.rxbinding:rxbinding-design:0.4.0',

        // Test dependencies
        junit              : 'junit:junit:4.12',
        truth              : 'com.google.truth:truth:0.27',
        robolectric        : 'org.robolectric:robolectric:2.2',
        compiletesting     : 'com.google.testing.compile:compile-testing:0.7',
        autoservice        : 'com.google.auto.service:auto-service:1.0-rc2',
        autocommon         : 'com.google.auto:auto-common:0.4'
]




